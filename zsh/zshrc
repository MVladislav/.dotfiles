# ~/.zshrc

# # when tmux user installed local bin will needed for plugins
# if [ -d "$HOME/.local/bin" ]; then
#   if [[ ":$PATH:" != *":$HOME/.local/bin:"* ]]; then
#     export PATH="$HOME/.local/bin:$PATH"
#   fi
# fi

# History settings
HISTFILE="$HOME/.zsh_history"     # Save them on this file
HISTSIZE=1000000                  # Set the amount of lines you want saved
SAVEHIST=1000000                  # This is required to actually save them, needs to match with HISTSIZE
setopt EXTENDED_HISTORY           # Write the history file in the ":start:elapsed;command" format.
setopt INC_APPEND_HISTORY         # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY              # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST     # Expire duplicate entries first when trimming history.
setopt HIST_IGNORE_DUPS           # Don't record an entry that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS       # Delete old recorded entry if new entry is a duplicate.
setopt HIST_FIND_NO_DUPS          # Do not display a line previously found.
setopt HIST_IGNORE_SPACE          # Don't record an entry starting with a space.
setopt HIST_SAVE_NO_DUPS          # Don't write duplicate entries in the history file.
setopt HIST_REDUCE_BLANKS         # Remove superfluous blanks before recording entry.

# Directory navigation
setopt AUTO_PUSHD                 # Makes cd automatically behave like pushd.
setopt PUSHD_IGNORE_DUPS          # Prevents duplicate entries in the directory stack.
setopt PUSHD_SILENT               # Suppresses the automatic printing of the directory stack after pushd or popd.
setopt AUTO_CD                    # Move to directories without cd

# ------------------------------------------------------------------------------

zsh_custom="$HOME/.zsh/custom"
zsh_custom_t="$zsh_custom/themes"
zsh_custom_p="$zsh_custom/plugins"

# Load the Spaceship theme
theme_spaceship="$zsh_custom_t/spaceship.zsh-theme"
[[ -e "$theme_spaceship" ]] && source "$theme_spaceship"
# Optional: Customize Spaceship theme (optional)
# https://spaceship-prompt.sh/config/prompt/#Prompt-order

# Syntax highlighting plugin
plugins_zsh_syntax_highlighting="$zsh_custom_p/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
[[ -e "$plugins_zsh_syntax_highlighting" ]] && source "$plugins_zsh_syntax_highlighting"
# Autosuggestions plugin
plugins_zsh_autosuggestions="$zsh_custom_p/zsh-autosuggestions/zsh-autosuggestions.zsh"
[[ -e "$plugins_zsh_autosuggestions" ]] && source "$plugins_zsh_autosuggestions"
# History search plugin
plugins_zsh_history_substring_search="$zsh_custom_p/zsh-history-substring-search/zsh-history-substring-search.zsh"
[[ -e "$plugins_zsh_history_substring_search" ]] && source "$plugins_zsh_history_substring_search"

# Enable fzf key bindings and fuzzy completion
command -v fzf &>/dev/null && source <(fzf --zsh)

# ------------------------------------------------------------------------------

# Set LS_COLORS for directory coloring
# https://geoff.greer.fm/lscolors/
export LSCOLORS="Gxfxcxdxbxegedabagacad"
export LS_COLORS='no=00:fi=00:di=01;34:ln=00;36:pi=40;33:so=01;35:do=01;35:bd=40;33;01:cd=40;33;01:or=41;33;01:ex=00;32:*.cmd=00;32:*.exe=01;32:*.com=01;32:*.bat=01;32:*.btm=01;32:*.dll=01;32:*.tar=00;31:*.tbz=00;31:*.tgz=00;31:*.rpm=00;31:*.deb=00;31:*.arj=00;31:*.taz=00;31:*.lzh=00;31:*.lzma=00;31:*.zip=00;31:*.zoo=00;31:*.z=00;31:*.Z=00;31:*.gz=00;31:*.bz2=00;31:*.tb2=00;31:*.tz2=00;31:*.tbz2=00;31:*.avi=01;35:*.bmp=01;35:*.fli=01;35:*.gif=01;35:*.jpg=01;35:*.jpeg=01;35:*.mng=01;35:*.mov=01;35:*.mpg=01;35:*.pcx=01;35:*.pbm=01;35:*.pgm=01;35:*.png=01;35:*.ppm=01;35:*.tga=01;35:*.tif=01;35:*.xbm=01;35:*.xpm=01;35:*.dl=01;35:*.gl=01;35:*.wmv=01;35:*.aiff=00;32:*.au=00;32:*.mid=00;32:*.mp3=00;32:*.ogg=00;32:*.voc=00;32:*.wav=00;32:'

# Completion system setup
autoload -Uz compinit && compinit

# Modify completion behavior with menu select and colors (`zstyle -L`)
zstyle ':completion:*' format '%B%F{yellow}%p%f%b'                                        # Highlight matched prefix in yellow
zstyle ':completion:*' menu select                                                        # Set number of items in the menu
# zstyle ':completion:*' accept-exact true                                                # Accept exact matches instead of directory change
zstyle ':completion:*' special-dirs true                                                  # Complete . and .. special directories
[[ -z "$LS_COLORS" ]] || zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"          # Use LS_COLORS for completion coloring
zstyle ':completion:*:cd:*' tag-order local-directories directory-stack path-directories  # disable named-directories autocompletion
zstyle '*' single-ignored show                                                            # ...
zstyle ':completion:*' use-cache true
zstyle ':completion:*' cache-path "$HOME/.zsh/cache"
zstyle ':completion:*' matcher-list 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' 'r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

zstyle ':completion:*' use-compctl false
zstyle ':completion:*:(ssh|scp|scp-r|sftp|rsh|rsync):*' hosts off
compdef scp-r=scp
compdef _scp scp-r

# Optional: Install and enable zsh-completions plugin for enhanced completion features
# git clone https://github.com/zsh-users/zsh-completions.git $ZSH_CUSTOM/plugins/zsh-completions
# plugins=(zsh-completions)

# ------------------------------------------------------------------------------

# Key bindings for history search
if [[ -n "$terminfo[kcuu1]" ]] && whence history-substring-search-up &>/dev/null; then
  bindkey -M emacs "$terminfo[kcuu1]" history-substring-search-up # history-search-backward
  bindkey -M viins "$terminfo[kcuu1]" history-substring-search-up # history-search-backward
fi
if [[ -n "$terminfo[kcud1]" ]] && whence history-substring-search-down &>/dev/null; then
  bindkey -M emacs "$terminfo[kcud1]" history-substring-search-down # history-search-forward
  bindkey -M viins "$terminfo[kcud1]" history-substring-search-down # history-search-forward
fi
HISTORY_SUBSTRING_SEARCH_ENSURE_UNIQUE=1

# ------------------------------------------------------------------------------

# Set up Alt+Backspace to delete words like in Bash
autoload -U select-word-style
select-word-style bash

# Try to load the backward-kill-word-match function
autoload -U backward-kill-word-match

# Only define the widget and bind it if it exists
if whence backward-kill-word-match &>/dev/null; then
  zle -N backward-kill-word backward-kill-word-match
  zstyle ':zle:backward-kill-word' word-style standard
  bindkey '^[^?' backward-kill-word # (Alt + Backspace)
fi

# ------------------------------------------------------------------------------

bindkey '^[[1;5D' backward-word   # move by word (Ctrl + Left)
bindkey '^[[1;5C' forward-word    # move by word (Ctrl + Right)
bindkey '^[[1;3D' backward-word   # move by word (Alt + Left)
bindkey '^[[1;3C' forward-word    # move by word (Alt + Right)
